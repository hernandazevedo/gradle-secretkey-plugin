apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'maven-publish'

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation gradleTestKit()
    testImplementation "junit:junit:4.12"
}

test {
    // Always run the tests
    outputs.upToDateWhen { false }

    // Turn on some console logging
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

//gradlew publishToMavenLocal
publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.hazevedo'
            artifactId = 'gradle-secretkey'
            version = '1.0'
            from components.java
        }
    }
}

configurations {
    deployerJars
}

uploadArchives {
    uploadArtifacts(project.name,repositories,configurations)
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

