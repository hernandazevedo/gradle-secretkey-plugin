apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

configurations {
    deployerJars
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile gradleTestKit()
    deployerJars "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.13"
}

test {
    // Always run the tests
    outputs.upToDateWhen { false }

    // Turn on some console logging
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

//gradlew publishToMavenLocal
publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.hazevedo'
            artifactId = 'gradle-secretkey'
            version = '1.0'

            from components.java
        }
    }
}

configurations {
    deployerJars
}

uploadArchives {
    Properties localProp = new Properties()
    String fileName = 'local.properties'
    if (project.rootProject.file(fileName).exists()) {
        localProp.load(new FileInputStream(rootProject.file(fileName)))
    }
    String packageCloudToken = System.env.PACKAGECLOUD_TOKEN != null ? System.env.PACKAGECLOUD_TOKEN : localProp["packageCloudToken"]
    repositories.mavenDeployer {
        pom.groupId = 'com.hazevedo'
        pom.artifactId = 'gradle-secretkey'
        pom.version = '1.0'
        configuration = configurations.deployerJars
        repository(url: "packagecloud+https://packagecloud.io/hernandazevedo/gradle-secretkey") {
            authentication(password: packageCloudToken)
        }
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

