apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile gradleTestKit()
}

test {
    // Always run the tests
    outputs.upToDateWhen { false }

    // Turn on some console logging
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.13"
}

//gradlew publishToMavenLocal
publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.hazevedo'
            artifactId = 'gradle-secretkey'
            version = '1.0'

            from components.java
        }
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

